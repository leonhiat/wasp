import Todo from "@ext/Todo.js"  // Imports non-wasp code from external code dir (ext/).

entity Task {
    isDone :: boolean,
    description :: string
}

app todoApp {
    title: "ToDo App"
}

page Main {
    route: "/",
    style: "@ext/Main.css",
    // TODO: We need to make this nicer / more explicit, it is not clear where is this coming from (these props).
    //   Also, this wiring is not elegant.
    content: {=jsx
        { /* Here we use Todo React component that we imported at the beginning of this file. */ }
        <Todo
            addTask={this.props.addTask}
            taskList={this.props.taskList}
            updateTask={this.props.updateTask}
            removeTask={this.props.removeTask}
        >
        </Todo>
    jsx=}
}

entity-form<Task> NewTaskForm {
    fields: {
        description: {
            show: true,
            label: none,
            placeholder: "What needs to be done?"
        },
        isDone: {
            show: false,
            defaultValue: false
        }
    },
    submit: {
        onEnter: true,
        button: { show: false }
    }
}

entity-list<Task> TaskList {
    showHeader: false,
    fields: {
        description: {
            // The contract for render is that user must provide a function that:
            // - Receives a task as an input.
            // - Returns a React Node or something that can be rendered by JSX.
            // - Does not depend on any outer context.
            render: {=js (task) => task.isDone ? <s>{task.description}</s> : task.description js=}
        }
    }
}
